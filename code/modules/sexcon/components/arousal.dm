/datum/component/arousal
	/// Our arousal level
	var/arousal = 0
	/// Arousal won't change if active
	var/arousal_frozen = FALSE
	/// Last time arousal increased
	var/last_arousal_increase_time = 0
	/// Last moan time for cooldowns
	var/last_moan = 0
	/// Last pain effect time
	var/last_pain = 0
	///our multiplier
	var/arousal_multiplier = 1
	/// Our charge gauge
	var/charge = SEX_MAX_CHARGE
	/// Last ejaculation time
	var/last_ejaculation_time = 0

/datum/component/arousal/Destroy(force)
	. = ..()
	STOP_PROCESSING(SSobj, src)

/datum/component/arousal/RegisterWithParent()
	. = ..()
	RegisterSignal(parent, COMSIG_SEX_ADJUST_AROUSAL, PROC_REF(adjust_arousal))
	RegisterSignal(parent, COMSIG_SEX_SET_AROUSAL, PROC_REF(set_arousal))
	RegisterSignal(parent, COMSIG_SEX_FREEZE_AROUSAL, PROC_REF(freeze_arousal))
	RegisterSignal(parent, COMSIG_SEX_GET_AROUSAL, PROC_REF(get_arousal))
	RegisterSignal(parent, COMSIG_SEX_RECEIVE_ACTION, PROC_REF(receive_sex_action))
	RegisterSignal(parent, COMSIG_MOB_CLIENT_LOGIN, PROC_REF(check_processing))
	RegisterSignal(parent, COMSIG_MOB_LOGOUT, PROC_REF(check_processing))

/datum/component/arousal/UnregisterFromParent()
	. = ..()
	UnregisterSignal(parent, COMSIG_SEX_ADJUST_AROUSAL)
	UnregisterSignal(parent, COMSIG_SEX_SET_AROUSAL)
	UnregisterSignal(parent, COMSIG_SEX_FREEZE_AROUSAL)
	UnregisterSignal(parent, COMSIG_SEX_GET_AROUSAL)
	UnregisterSignal(parent, COMSIG_SEX_RECEIVE_ACTION)
	UnregisterSignal(parent, COMSIG_MOB_CLIENT_LOGIN)
	UnregisterSignal(parent, COMSIG_MOB_LOGOUT)

/datum/component/arousal/process(dt)
	handle_charge(dt * 1)
	if(!can_lose_arousal())
		return
	adjust_arousal(parent, dt * -1)

/// Checks if our parent has a client and adjusts processing.
/datum/component/arousal/proc/check_processing()
	SIGNAL_HANDLER
	var/mob/parent_mob = parent
	if(parent_mob.client)
		START_PROCESSING(SSobj, src)
	else
		STOP_PROCESSING(SSobj, src)

/datum/component/arousal/proc/can_lose_arousal()
	if(last_arousal_increase_time + AROUSAL_TIME_TO_UNHORNY > world.time)
		return FALSE
	return TRUE

/datum/component/arousal/proc/set_arousal(datum/source, amount, forced = FALSE)
	if(amount > arousal)
		last_arousal_increase_time = world.time
	// Thrillseekers used to be clamped below the ejaculation thresholds here, which meant they could
	// never climax at all. They now use the same ceiling as everyone else; the flaw's identity is that
	// combat also arouses them (see adjust_arousal_special) and that fighting is what sates the vice.
	arousal = clamp(amount, 0, MAX_AROUSAL)
	update_arousal_effects()
	try_ejaculate()
	SEND_SIGNAL(parent, COMSIG_SEX_AROUSAL_CHANGED)
	return arousal

/datum/component/arousal/proc/adjust_arousal(datum/source, amount, forced = FALSE)
	if(arousal_frozen)
		return arousal
	if(arousal > 0)
		arousal *= arousal_multiplier
	return set_arousal(source, arousal + amount, forced)

/datum/component/arousal/proc/adjust_arousal_special(datum/source, amount, forced = FALSE)
	var/mob/living/mob = parent
	if(!mob.has_flaw(/datum/charflaw/addiction/thrillseeker))
		return
	if(arousal_frozen)
		return arousal
	if(arousal > 0)
		arousal *= arousal_multiplier
	return set_arousal_special(source, arousal + amount)

/// Arousal gained from combat rather than sex. Same ceiling and climax rules as set_arousal(); the
/// only difference is the refractory window, so a long fight doesn't retrigger endlessly.
/datum/component/arousal/proc/set_arousal_special(datum/source, amount, limit)
	if(last_ejaculation_time > world.time - (3 MINUTES))	//Short break to not cover the screen in pink too quickly.
		return
	if(amount > arousal)
		last_arousal_increase_time = world.time
	var/clamp_max = MAX_AROUSAL
	if(limit)
		clamp_max = limit
	arousal = clamp(amount, 0, clamp_max)
	update_arousal_effects()
	try_ejaculate()
	SEND_SIGNAL(parent, COMSIG_SEX_AROUSAL_CHANGED)
	return arousal

/datum/component/arousal/proc/freeze_arousal(datum/source, freeze_state = null)
	if(freeze_state == null)
		arousal_frozen = !arousal_frozen
	else
		arousal_frozen = freeze_state
	return arousal_frozen

/datum/component/arousal/proc/get_arousal(datum/source, list/arousal_data)
	arousal_data += list(
		"arousal" = arousal,
		"frozen" = arousal_frozen,
		"last_increase" = last_arousal_increase_time,
		"arousal_multiplier" = arousal_multiplier
	)

/datum/component/arousal/proc/receive_sex_action(datum/source, arousal_amt, pain_amt, giving, applied_force, applied_speed)
	var/mob/user = parent

	// Apply multipliers
	arousal_amt *= get_force_pleasure_multiplier(applied_force, giving)
	pain_amt *= get_force_pain_multiplier(applied_force)
	pain_amt *= get_speed_pain_multiplier(applied_speed)

	if(user.stat == DEAD)
		arousal_amt = 0
		pain_amt = 0

	if(!arousal_frozen)
		adjust_arousal(source, arousal_amt)

	damage_from_pain(pain_amt)
	try_do_moan(arousal_amt, pain_amt, applied_force, giving)
	try_do_pain_effect(pain_amt, giving)

/datum/component/arousal/proc/update_arousal_effects()
	update_pink_screen()
	update_blueballs()
	update_erect_state()

/datum/component/arousal/proc/try_ejaculate()
	if(arousal < PASSIVE_EJAC_THRESHOLD)
		return
	if(is_spent())
		return
	ejaculate()
	record_round_statistic(STATS_PLEASURES)

/datum/component/arousal/proc/ejaculate()
	var/mob/living/mob = parent
	var/list/parent_sessions = return_sessions_with_user(parent)
	var/datum/sex_session/highest_priority = return_highest_priority_action(parent_sessions, parent)
	// No session, or none with a running action - climaxing alone. The old code dereferenced
	// highest_priority here and only checked it for null fifteen lines further down, so every
	// sessionless climax runtimed instead of happening.
	var/mob/living/carbon/human/climaxer = ishuman(parent) ? parent : null
	var/mob/living/carbon/human/partner
	var/datum/sex_action/action

	if(highest_priority)
		action = SEX_ACTION(highest_priority.current_action)
		if(action?.flipped)
			climaxer = highest_priority.target
			partner = highest_priority.user
		else
			climaxer = highest_priority.user
			partner = highest_priority.target

	if(!climaxer)
		return

	playsound(parent, 'sound/misc/mat/endout.ogg', 50, TRUE, ignore_walls = FALSE)
	// Special case for when the climaxer has a penis but no testicles
	if(!mob.getorganslot(ORGAN_SLOT_TESTICLES) && mob.getorganslot(ORGAN_SLOT_PENIS))
		mob.visible_message(span_love("[mob] climaxes, yet nothing is released!"))
		after_ejaculation(action, climaxer, partner)
		return
	if(!action)
		mob.visible_message(span_love("[mob] makes a mess!"))
		var/turf/turf = get_turf(parent)
		for(var/i in 1 to get_load_bursts())
			new /obj/effect/decal/cleanable/coom(turf)
		after_ejaculation(action, climaxer, partner)
	else
		var/return_message = action.handle_climax_message(climaxer, partner)
		if(!return_message)
			mob.visible_message(span_love("[mob] makes a mess!"))
			var/turf/turf = get_turf(parent)
			for(var/i in 1 to get_load_bursts())
				new /obj/effect/decal/cleanable/coom(turf)
			after_ejaculation(action, climaxer, partner)
		else
			handle_climax(return_message, climaxer, partner, action)
		if(action.knot_on_finish)
			action.try_knot_on_climax(mob, partner)

//VALMORIAN: milking-into-a-container, ported from Emerald Summit's sex_controller. ES kept these on
//its monolithic sexcon; here they live with the rest of the climax handling.
/datum/component/arousal/proc/get_semen_volume()
	var/mob/living/mob = parent
	var/obj/item/organ/testicles/testes = mob.getorganslot(ORGAN_SLOT_TESTICLES)
	if(!testes)
		return 0
	var/volume
	switch(testes.ball_size)
		if(MIN_TESTICLES_SIZE)
			volume = 2
		if(MAX_TESTICLES_SIZE)
			volume = 4
		else
			volume = 3
	if(HAS_TRAIT(mob, TRAIT_GOODLOVER))
		volume = floor(volume * 1.5)

	var/obj/item/organ/penis/shaft = mob.getorganslot(ORGAN_SLOT_PENIS)
	//VALMORIAN: ES also listed EQUINE_KNOTTED and TAPERED_KNOTTED; VI's penis type enum has neither.
	if(shaft?.penis_type in list(PENIS_TYPE_KNOTTED, PENIS_TYPE_EQUINE, PENIS_TYPE_TAPERED_DOUBLE_KNOTTED, PENIS_TYPE_BARBED_KNOTTED))
		volume += 1

	return volume

///Number of cum bursts (extra floor puddles) per climax, scaled by semen volume - ES's get_load_bursts(), reworked for VI's floor-decal mess instead of a spurt animation.
/datum/component/arousal/proc/get_load_bursts()
	switch(get_semen_volume())
		if(4)
			return 2
		if(5 to INFINITY)
			return 3
		else
			return 1

///How many climaxes worth of charge we can hold, scaled by testicle size, CON, the GOODLOVER/BIGGUY traits, and gnoll species - ES's get_max_loads(), "cum con".
/datum/component/arousal/proc/get_max_loads()
	var/mob/living/mob = parent
	var/con = mob.STACON
	var/minimum_loads = 3
	var/obj/item/organ/testicles/testes = mob.getorganslot(ORGAN_SLOT_TESTICLES)
	if(testes)
		switch(testes.ball_size)
			if(MIN_TESTICLES_SIZE)
				minimum_loads = 2
			if(MAX_TESTICLES_SIZE)
				minimum_loads = 4
	var/loads = minimum_loads + floor(clamp((con - 10) * 2, 0, 99) / 2)
	if(HAS_TRAIT(mob, TRAIT_GOODLOVER))
		loads *= 1.5
	if(HAS_TRAIT(mob, TRAIT_BIGGUY))
		loads *= 1.5
	if(is_species(mob, /datum/species/gnoll))
		loads *= 1.5
	return floor(loads)

///Max charge based on the dynamic load count above, replacing the old flat SEX_MAX_CHARGE.
/datum/component/arousal/proc/get_max_charge()
	return get_max_loads() * CHARGE_FOR_CLIMAX

/datum/component/arousal/proc/can_ejaculate()
	var/mob/living/mob = parent
	if(!mob.getorganslot(ORGAN_SLOT_TESTICLES) && !mob.getorganslot(ORGAN_SLOT_VAGINA))
		return FALSE
	if(HAS_TRAIT(mob, TRAIT_LIMPDICK))
		return FALSE
	return TRUE

/datum/component/arousal/proc/check_active_ejaculation()
	if(arousal < ACTIVE_EJAC_THRESHOLD)
		return FALSE
	if(is_spent())
		return FALSE
	if(!can_ejaculate())
		return FALSE
	return TRUE

/datum/component/arousal/proc/ejaculate_container(obj/item/reagent_containers/glass/container)
	var/mob/living/carbon/human/mob = parent
	if(!istype(mob) || !istype(container))
		return
	log_combat(mob, mob, "Ejaculated into a container")
	mob.visible_message(span_love("[mob] spills into [container]!"))
	playsound(mob, 'sound/misc/mat/endout.ogg', 50, TRUE, ignore_walls = FALSE)
	if(mob.getorganslot(ORGAN_SLOT_PENIS))
		container.reagents.add_reagent(/datum/reagent/erpjuice/cum, get_semen_volume())
	else
		container.reagents.add_reagent(/datum/reagent/erpjuice/femcum, 2)
	after_ejaculation(null, mob, null)

/datum/component/arousal/proc/handle_cock_milking(mob/living/carbon/human/milker)
	if(!check_active_ejaculation())
		return
	ejaculate_container(milker.get_active_held_item())

/datum/component/arousal/proc/ejaculate_special()
	var/mob/living/mob = parent
	after_ejaculation_special(mob)
	last_ejaculation_time = world.time

/datum/component/arousal/proc/after_ejaculation_special(mob/living/parent)
	parent.add_stress(/datum/stressevent/thrill)
	if(prob(1))
		parent.emote("groan", forced = TRUE)

/datum/component/arousal/proc/handle_climax(climax_type, mob/living/carbon/human/climaxer, mob/living/carbon/human/partner, action)
	// The container actions climax through here like everything else - try_ejaculate() fires the
	// moment arousal crosses the passive threshold, so waiting for an explicit milking call after
	// the fact never worked (the generic path had already spent the climax on the floor). The
	// container is always in the hand of whoever is doing the milking: the session user.
	if(climax_type == "container")
		var/obj/item/reagent_containers/glass/container = climaxer?.get_active_held_item()
		if(istype(container))
			ejaculate_container(container)
			return
		climax_type = "self" //Container vanished mid-act - plain mess instead.

	var/bursts = get_load_bursts()

	switch(climax_type)
		if("onto")
			log_combat(climaxer, partner, "Came onto [partner]")
			playsound(partner, 'sound/misc/mat/endout.ogg', 50, TRUE, ignore_walls = FALSE)
			var/turf/turf = get_turf(partner)
			for(var/i in 1 to bursts)
				new /obj/effect/decal/cleanable/coom(turf)
			apply_cum_onto(partner, istype(action, /datum/sex_action/oral/crotch_nuzzle))
		if("into")
			log_combat(climaxer, partner, "Came inside [partner]")
			playsound(partner, 'sound/misc/mat/endin.ogg', 50, TRUE, ignore_walls = FALSE)
			apply_cum_into(climaxer, partner, get_climax_zone(action))
		if("self")
			log_combat(climaxer, climaxer, "Ejaculated")
			climaxer.visible_message(span_love("[climaxer] makes a mess!"))
			playsound(climaxer, 'sound/misc/mat/endout.ogg', 50, TRUE, ignore_walls = FALSE)
			var/turf/turf = get_turf(partner)
			for(var/i in 1 to bursts)
				new /obj/effect/decal/cleanable/coom(turf)

	after_ejaculation(action, climaxer, partner)

//VALMORIAN: the mess/examine-text system, ported from ES's cum_onto()/cum_into(). ES's sexcon knew
//the exact orifice a climax landed in; VI's per-action sexcon2 also knows this (see each action's
//handle_climax_message) but discards it down to "into"/"onto"/"self" before reaching this proc - so
//get_climax_zone() below recovers it via istype family instead of touching every action file.

///Applies (or refreshes) the face/body cum status effect for a splash landing outside an orifice.
/datum/component/arousal/proc/apply_cum_onto(mob/living/carbon/human/splashed, on_face)
	if(!splashed)
		return
	var/effect_type = on_face ? /datum/status_effect/facial : /datum/status_effect/facial/external
	var/datum/status_effect/facial/existing = splashed.has_status_effect(effect_type)
	if(existing)
		existing.refresh_cum()
	else
		splashed.apply_status_effect(effect_type)

///Applies the internal creampie status effect + drip + reagent for a climax landing inside an orifice.
/datum/component/arousal/proc/apply_cum_into(mob/living/carbon/human/climaxer, mob/living/carbon/human/splashed, zone)
	if(!splashed || !zone)
		return
	var/oral = (zone == SEX_PART_MOUTH)
	var/effect_type = oral ? /datum/status_effect/facial : /datum/status_effect/facial/internal
	var/datum/status_effect/facial/existing = splashed.has_status_effect(effect_type)
	if(existing)
		existing.refresh_cum()
	else
		splashed.apply_status_effect(effect_type)
	if(splashed.reagents)
		if(climaxer.getorganslot(ORGAN_SLOT_PENIS))
			splashed.reagents.add_reagent(/datum/reagent/erpjuice/cum, get_semen_volume())
		else
			splashed.reagents.add_reagent(/datum/reagent/erpjuice/femcum, 2)
	if(!oral)
		var/obj/item/organ/testicles/testes = climaxer.getorganslot(ORGAN_SLOT_TESTICLES)
		apply_creampie_drip(splashed, zone, testes?.ball_size > DEFAULT_TESTICLES_SIZE)

///Buckets a sex_action by which orifice a creampie lands in. Ambiguous/ES-less cases (knot_grinding,
//VI-only tailmaw penetration) default to the most common bucket rather than inventing a new one.
/datum/component/arousal/proc/get_climax_zone(datum/sex_action/action)
	if(!action)
		return SEX_PART_MOUTH
	if(istype(action, /datum/sex_action/oral/blowjob) || istype(action, /datum/sex_action/force_blowjob) || istype(action, /datum/sex_action/sex/throat) || istype(action, /datum/sex_action/oral/cunnilingus) || istype(action, /datum/sex_action/tailmaw_blowjob) || istype(action, /datum/sex_action/tailmaw_cunnilingus))
		return SEX_PART_MOUTH
	if(istype(action, /datum/sex_action/sex/double_penetration))
		return (SEX_PART_CUNT | SEX_PART_ANUS)
	if(istype(action, /datum/sex_action/sex/anal) || istype(action, /datum/sex_action/sex/anal_ride) || istype(action, /datum/sex_action/sex/other/anal))
		return SEX_PART_ANUS
	if(istype(action, /datum/sex_action/sex/slit))
		return SEX_PART_SLIT_SHEATH
	return SEX_PART_CUNT

/datum/component/arousal/proc/after_ejaculation(datum/sex_action/action, mob/living/carbon/human/climaxer, mob/living/carbon/human/partner)
	SEND_SIGNAL(climaxer, COMSIG_SEX_SET_AROUSAL, 20)
	SEND_SIGNAL(climaxer, COMSIG_SEX_CLIMAX)

	charge = max(0, charge - CHARGE_FOR_CLIMAX)

	var/intensity
	if(action)
		intensity = action.intensity
		if(!action.masturbation) //If the action's masturbation, no good lover bonus
			//HAS_TRAIT dereferences its target, and a solo climax has no partner
			if(partner && HAS_TRAIT(partner, TRAIT_GOODLOVER)) //If your partner is a good lover, your climax is more intense
				intensity += 1

	// Thrillseekers previously bailed out here with a consolation-prize stress event and none of the
	// benefits below. They now climax like anyone else - fighting is still what sates their vice
	// (see bodypart_wounds.dm), sex simply isn't worthless to them any more.
	climaxer.emote("moan", forced = TRUE)
	climaxer.playsound_local(climaxer, 'sound/misc/mat/end.ogg', 100)
	last_ejaculation_time = world.time

	if(HAS_TRAIT(climaxer, TRAIT_UNSATISFIED)) //Given for 30 seconds when someone sets their arousal, it prevents gaining any benefits from orgasm
		return

	climaxer.sate_addiction(/datum/charflaw/addiction/lovefiend)
	// Solo climaxes have no partner - see ejaculate().
	partner?.sate_addiction(/datum/charflaw/addiction/lovefiend)

	switch(intensity)
		if(1) //Should only be achievable with masturbation
			climaxer.add_stress(/datum/stressevent/cumself)
		if(2)
			climaxer.add_stress(/datum/stressevent/cumok)
		if(3)
			climaxer.add_stress(/datum/stressevent/cummid)
		if(4)
			climaxer.add_stress(/datum/stressevent/cumgood)
		if(5) //Should only be achievable with a good lover and a normally intimate action
			climaxer.add_stress(/datum/stressevent/cummax)
		else //This should not trigger but just in case
			climaxer.add_stress(/datum/stressevent/cumok)

	if(partner && HAS_TRAIT(partner, TRAIT_GOODLOVER) && intensity >= 4)
		if(!climaxer.mob_timers["cumtri"])
			climaxer.mob_timers["cumtri"] = world.time
			climaxer.adjust_triumphs(1)
			to_chat(climaxer, span_love("Our loving is a true TRIUMPH!"))
		if(!partner.mob_timers["cumtri"])
			partner.mob_timers["cumtri"] = world.time
			partner.adjust_triumphs(1)
			to_chat(partner, span_love("Our loving is a true TRIUMPH!"))

	if(partner)
		var/user_beautiful = HAS_TRAIT(climaxer, TRAIT_BEAUTIFUL)
		var/user_ugly = HAS_TRAIT(climaxer, TRAIT_UNSEEMLY) || HAS_TRAIT(climaxer, TRAIT_DISFIGURED)
		var/target_beautiful = HAS_TRAIT(partner, TRAIT_BEAUTIFUL)
		var/target_ugly = HAS_TRAIT(partner, TRAIT_UNSEEMLY) || HAS_TRAIT(partner, TRAIT_DISFIGURED)
		if((user_ugly && target_ugly) || (user_beautiful && target_beautiful))
			climaxer.add_stress(/datum/stressevent/cummax)
			partner.add_stress(/datum/stressevent/cummax)
		else
			var/user_goodlover = HAS_TRAIT(climaxer, TRAIT_GOODLOVER)
			var/target_goodlover = HAS_TRAIT(partner, TRAIT_GOODLOVER)
			if(target_ugly && !user_ugly && !user_goodlover)
				if(user_beautiful)
					climaxer.add_stress(/datum/stressevent/unseemly_made_love/beautiful)
				else
					climaxer.add_stress(/datum/stressevent/unseemly_made_love)
				partner.add_stress(/datum/stressevent/cummax)
			if(user_ugly && !target_ugly && !target_goodlover)
				if(target_beautiful)
					partner.add_stress(/datum/stressevent/unseemly_made_love/beautiful)
				else
					partner.add_stress(/datum/stressevent/unseemly_made_love)
				climaxer.add_stress(/datum/stressevent/cummax)

	if(partner && action?.intensity >= 4 && climaxer.has_flaw(/datum/charflaw/addiction/sadist))
		partner.emote("paincrit", forced = TRUE)


/datum/component/arousal/proc/set_charge(amount)
	var/empty = (charge < CHARGE_FOR_CLIMAX)
	charge = clamp(amount, 0, get_max_charge())
	var/after_empty = (charge < CHARGE_FOR_CLIMAX)
	if(empty && !after_empty)
		to_chat(parent, span_notice("I feel like I'm not so spent anymore"))
	if(!empty && after_empty)
		to_chat(parent, span_notice("I'm spent!"))

/datum/component/arousal/proc/adjust_charge(amount)
	set_charge(charge + amount)

/datum/component/arousal/proc/handle_charge(dt)
	adjust_charge(dt * CHARGE_RECHARGE_RATE)
	if(is_spent())
		if(arousal > 60)
			to_chat(parent, span_warning("I'm too spent!"))
			adjust_arousal(parent, -20)
			return
		adjust_arousal(parent, -dt * SPENT_AROUSAL_RATE)

/datum/component/arousal/proc/is_spent()
	if(charge < CHARGE_FOR_CLIMAX)
		return TRUE
	return FALSE

/datum/component/arousal/proc/update_pink_screen()
	var/mob/user = parent
	var/severity = min(10, CEILING(arousal * 0.1, 1))
	if(severity > 0)
		user.overlay_fullscreen("horny", /atom/movable/screen/fullscreen/love, severity)
	else
		user.clear_fullscreen("horny")

/datum/component/arousal/proc/update_blueballs()
	var/mob/user = parent
	if(last_arousal_increase_time + 30 SECONDS > world.time)
		return
	if(arousal >= BLUEBALLS_GAIN_THRESHOLD)
		user.add_stress(/datum/stressevent/blue_balls)
	else if(arousal <= BLUEBALLS_LOOSE_THRESHOLD)
		user.remove_stress(/datum/stressevent/blue_balls)

/datum/component/arousal/proc/update_erect_state()


/datum/component/arousal/proc/damage_from_pain(pain_amt)
	var/mob/living/carbon/user = parent
	if(pain_amt < PAIN_MINIMUM_FOR_DAMAGE)
		return
	var/damage = (pain_amt / PAIN_DAMAGE_DIVISOR)
	var/obj/item/bodypart/part = user.get_bodypart(BODY_ZONE_CHEST)
	if(!part)
		return
	user.apply_damage(damage, BRUTE, part)

/datum/component/arousal/proc/try_do_moan(arousal_amt, pain_amt, applied_force, giving)
	var/mob/user = parent
	if(arousal_amt < 1.5)
		return
	if(user.stat != CONSCIOUS)
		return
	if(last_moan + MOAN_COOLDOWN >= world.time)
		return
	if(prob(50))
		return
	var/chosen_emote
	switch(arousal_amt)
		if(0 to 5)
			chosen_emote = "sexmoanlight"
		if(5 to INFINITY)
			chosen_emote = "sexmoanhvy"

	if(pain_amt >= PAIN_MILD_EFFECT)
		if(giving)
			if(prob(30))
				chosen_emote = "groan"
		else
			if(prob(40))
				chosen_emote = "painmoan"
	if(pain_amt >= PAIN_MED_EFFECT)
		if(giving)
			if(prob(50))
				chosen_emote = "groan"
		else
			if(prob(60))
				chosen_emote = "painmoan"

	last_moan = world.time
	user.emote(chosen_emote)

/datum/component/arousal/proc/try_do_pain_effect(pain_amt, giving)
	var/mob/user = parent
	if(pain_amt < PAIN_MILD_EFFECT)
		return
	if(last_pain + PAIN_COOLDOWN >= world.time)
		return
	if(prob(50))
		return
	last_pain = world.time
	if(pain_amt >= PAIN_HIGH_EFFECT)
		var/pain_msg = pick(list("IT HURTS!!!", "IT NEEDS TO STOP!!!", "I CAN'T TAKE IT ANYMORE!!!"))
		to_chat(user, span_boldwarning(pain_msg))
		if(user.show_redflash())
			user.flash_fullscreen("redflash2")
		if(prob(70) && user.stat == CONSCIOUS)
			user.visible_message(span_warning("[user] shudders in pain!"))
	else if(pain_amt >= PAIN_MED_EFFECT)
		var/pain_msg = pick(list("It hurts!", "It pains me!"))
		to_chat(user, span_boldwarning(pain_msg))
		if(user.show_redflash())
			user.flash_fullscreen("redflash1")
		if(prob(40) && user.stat == CONSCIOUS)
			user.visible_message(span_warning("[user] shudders in pain!"))
	else
		var/pain_msg = pick(list("It hurts a little...", "It stings...", "I'm aching..."))
		to_chat(user, span_warning(pain_msg))

/datum/component/arousal/proc/get_force_pleasure_multiplier(passed_force, giving)
	switch(passed_force)
		if(SEX_FORCE_LOW)
			if(giving)
				return 0.8
			else
				return 0.8
		if(SEX_FORCE_MID)
			if(giving)
				return 1.2
			else
				return 1.2
		if(SEX_FORCE_HIGH)
			if(giving)
				return 1.6
			else
				return 1.2
		if(SEX_FORCE_EXTREME)
			if(giving)
				return 2.0
			else
				return 0.8
		if(SEX_FORCE_LUDICROUS)
			if(giving)
				return 2.0
			else
				return 0.8

/datum/component/arousal/proc/get_force_pain_multiplier(passed_force)
	switch(passed_force)
		if(SEX_FORCE_LOW)
			return 0.5
		if(SEX_FORCE_MID)
			return 1.0
		if(SEX_FORCE_HIGH)
			return 2.0
		if(SEX_FORCE_EXTREME)
			return 3.0
		if(SEX_FORCE_LUDICROUS)
			return 4.0

/datum/component/arousal/proc/get_speed_pain_multiplier(passed_speed)
	switch(passed_speed)
		if(SEX_SPEED_LOW)
			return 0.8
		if(SEX_SPEED_MID)
			return 1.0
		if(SEX_SPEED_HIGH)
			return 1.2
		if(SEX_SPEED_EXTREME)
			return 1.4
		if(SEX_SPEED_LUDICROUS)
			return 1.6
