/datum/job/roguetown/gnoll
	title = "Gnoll"
	flag = GNOLL
	department_flag = ANTAGONIST
	antag_job = TRUE // whoever wrote this, I'm- gghrhhah!
	faction = "Station"
	total_positions = 0
	spawn_positions = 0
	forbidden_races = list(RACES_CONSTRUCT RACES_DESPISED)
	tutorial = "You have proven yourself worthy to Graggar, and he's granted you his blessing most divine. Now you hunt for worthy opponents, seeking out those strong enough to make you bleed."
	outfit = null
	outfit_female = null
	display_order = JDO_GNOLL
	show_in_credits = TRUE
	min_pq = 10
	max_pq = null
	allowed_patrons = list(/datum/patron/inhumen/graggar)

	obsfuscated_job = TRUE

	advclass_cat_rolls = list(CTAG_GNOLL = 20)
	PQ_boost_divider = 10
	round_contrib_points = 2

	announce_latejoin = FALSE
	wanderer_examine = TRUE
	advjob_examine = TRUE
	always_show_on_latechoices = TRUE
	job_reopens_slots_on_death = FALSE
	same_job_respawn_delay = 1 MINUTES
	virtue_restrictions = list(
		/datum/virtue/utility/noble,
		/datum/virtue/combat/dualwielder, //Claws are too powerful, abusable
		/datum/virtue/background/duelist, //They do not need weapon skills or anything in here
		/datum/virtue/background/executioner,
		/datum/virtue/background/militia,
		/datum/virtue/background/brawler,
		/datum/virtue/utility/notable, //No resident (????) or free-money-stash gnolls
		/datum/virtue/utility/bronzelimbs, //They should feel pain in their limbs given their state
		/datum/virtue/movement/acrobatic, //This should be given to them when they are actually after a Hunted
		/datum/virtue/utility/woodwalker, //This should be given to them when they are actually after a Hunted
		/datum/virtue/background/bowman	//Absolutely not on a class like this
		)
	job_subclasses = list(
		/datum/advclass/gnoll/berserker,
		/datum/advclass/gnoll/knight,
		/datum/advclass/gnoll/templar,
		/datum/advclass/gnoll/shaman,
	)
	vice_restrictions = list(/datum/charflaw/hunted, /datum/charflaw/targeted)

/datum/advclass/gnoll
	tempo_capable = FALSE

/datum/job/roguetown/gnoll/special_job_check(mob/dead/new_player/player)
	if(is_storyteller_soft_antag_blocked())
		return FALSE
	return ..()

/datum/job/roguetown/gnoll/special_check_latejoin(client/C)
	if(is_storyteller_soft_antag_blocked())
		return FALSE
	return ..()

/datum/job/roguetown/gnoll/after_spawn(mob/living/L, mob/M, latejoin = TRUE)
	..()
	if(L)
		var/mob/living/carbon/human/H = L
		// Assign wretch antagonist datum so wretches appear in antag list
		if(H.mind && !H.mind.has_antag_datum(/datum/antagonist/gnoll))
			var/datum/antagonist/new_antag = new /datum/antagonist/gnoll()
			H.mind.add_antag_datum(new_antag)
			add_verb(H, /mob/living/carbon/human/proc/gnoll_inspect_skin)

/mob/living/carbon/human/proc/reset_gnoll_sprite_scale()
	if(!dna?.features)
		return FALSE
	dna.features["body_size"] = BODY_SIZE_NORMAL
	dna.update_body_size()
	return TRUE

/// Applies the player's gnoll customization (client-level /datum/gnoll_prefs) to this mob:
/// name, pelt, genitals, descriptors, and gnoll-specific examine/OOC metadata. Ported from
/// Emerald Summit. Gnolls are anonymous antagonists — every examine field is wiped first so a
/// blank gnoll field shows nothing, never the base slot's flavortext/headshot/notes/music.
/mob/living/carbon/human/proc/apply_gnoll_preferences(initial_setup = TRUE)
	if(!client?.prefs?.gnoll_prefs)
		return FALSE

	reset_gnoll_sprite_scale()

	if(initial_setup)
		// Gnolls should be a blank slate at spawn; strip inherited charflaw state.
		// (Unlike ES there is no mob-level statpack here; stats are rerolled below.)
		charflaws = list()

	if(status_traits)
		for(var/trait in status_traits.Copy())
			if(HAS_TRAIT_FROM(src, trait, TRAIT_VIRTUE))
				REMOVE_TRAIT(src, trait, TRAIT_VIRTUE)

	var/datum/gnoll_prefs/gprefs = client.prefs.gnoll_prefs

	flavortext = null
	flavortext_cached = ""
	ooc_notes = null
	ooc_notes_cached = ""
	rumour = null
	noble_gossip = null
	headshot_link = null
	standard_headshot_link = null
	nsfwflavortext = null
	nsfwflavortext_cached = ""
	erpprefs = null
	erpprefs_cached = ""
	ooc_extra = null
	song_title = null
	song_artist = null
	img_gallery = list()
	nsfw_img_gallery = list()

	if(gprefs.gnoll_flavortext)
		flavortext = gprefs.gnoll_flavortext
		flavortext_cached = gprefs.gnoll_flavortext_cached
	if(gprefs.gnoll_ooc_notes)
		ooc_notes = gprefs.gnoll_ooc_notes
		ooc_notes_cached = gprefs.gnoll_ooc_notes_cached
	if(gprefs.gnoll_headshot_link)
		headshot_link = gprefs.gnoll_headshot_link
		standard_headshot_link = gprefs.gnoll_headshot_link
	if(gprefs.gnoll_nsfwflavortext)
		nsfwflavortext = gprefs.gnoll_nsfwflavortext
		nsfwflavortext_cached = gprefs.gnoll_nsfwflavortext_cached
	if(gprefs.gnoll_erpprefs)
		erpprefs = gprefs.gnoll_erpprefs
		erpprefs_cached = gprefs.gnoll_erpprefs_cached
	if(gprefs.gnoll_ooc_extra)
		ooc_extra = gprefs.gnoll_ooc_extra
	if(gprefs.gnoll_song_title)
		song_title = gprefs.gnoll_song_title
	if(gprefs.gnoll_song_artist)
		song_artist = gprefs.gnoll_song_artist
	if(length(gprefs.gnoll_img_gallery))
		img_gallery = gprefs.gnoll_img_gallery.Copy()
	if(length(gprefs.gnoll_nsfw_img_gallery))
		nsfw_img_gallery = gprefs.gnoll_nsfw_img_gallery.Copy()

	// Gnolls get their own subclass statlines in the equip flow; wipe the inherited statpack roll at spawn only.
	if(initial_setup)
		roll_stats()

	fully_replace_character_name(real_name, gprefs.ensure_gnoll_name())

	icon_state = gprefs.pelt_type || "firepelt"
	dna?.species?.custom_base_icon = gprefs.pelt_type || "firepelt"

	var/wants_penis = !!gprefs.genitals["penis"]
	var/wants_vagina = !!gprefs.genitals["vagina"]
	var/wants_breasts = !!gprefs.genitals["breasts"]

	var/obj/item/organ/penis/penis = getorganslot(ORGAN_SLOT_PENIS)
	if(wants_penis)
		if(!penis)
			penis = new /obj/item/organ/penis/knotted/big()
			penis.Insert(src, TRUE, FALSE)
		var/obj/item/organ/testicles/testicles = getorganslot(ORGAN_SLOT_TESTICLES)
		if(!testicles)
			testicles = new()
			testicles.Insert(src, TRUE, FALSE)
	else if(penis)
		penis.Remove(src)
		qdel(penis)
		var/obj/item/organ/testicles/testicles = getorganslot(ORGAN_SLOT_TESTICLES)
		if(testicles)
			testicles.Remove(src)
			qdel(testicles)

	var/obj/item/organ/vagina/vagina = getorganslot(ORGAN_SLOT_VAGINA)
	if(wants_vagina)
		if(!vagina)
			vagina = new /obj/item/organ/vagina()
			vagina.accessory_type = /datum/sprite_accessory/vagina/furred
			vagina.Insert(src, TRUE, FALSE)
	else if(vagina)
		vagina.Remove(src)
		qdel(vagina)

	var/obj/item/organ/breasts/breasts = getorganslot(ORGAN_SLOT_BREASTS)
	if(wants_breasts)
		if(!breasts)
			breasts = new()
			breasts.Insert(src, TRUE, FALSE)
	else if(breasts)
		breasts.Remove(src)
		qdel(breasts)

	update_body()
	ambushable = FALSE
	clear_mob_descriptors()
	add_mob_descriptor(/datum/mob_descriptor/stature/gnoll)
	add_mob_descriptor(gprefs.descriptor_height || /datum/mob_descriptor/height/moderate)
	add_mob_descriptor(gprefs.descriptor_body || /datum/mob_descriptor/body/muscular)
	add_mob_descriptor(gprefs.descriptor_fur || /datum/mob_descriptor/fur/coarse)
	add_mob_descriptor(gprefs.descriptor_voice || /datum/mob_descriptor/voice/growly)
	add_mob_descriptor(gprefs.descriptor_muzzle || /datum/mob_descriptor/face/gnoll/long_muzzle)
	add_mob_descriptor(gprefs.descriptor_expression || /datum/mob_descriptor/face_exp/gnoll/alert)
	return TRUE

/datum/outfit/job/roguetown/gnoll/proc/don_pelt(mob/living/carbon/human/H)
	if(H.mind)
		H.set_blindness(0)
		// Pelt, name, genitals and descriptors come from the lobby Gnoll Customization menu.
		if(!H.apply_gnoll_preferences())
			var/pelts = list("firepelt", "rotpelt", "whitepelt", "bloodpelt", "nightpelt", "darkpelt")
			var/pelt_choice = input(H, "Choose your pelt.", "SPILL THEIR ENTRAILS.") as anything in pelts
			H.icon_state = "[pelt_choice]"
			H.dna?.species?.custom_base_icon = "[pelt_choice]"
		H.regenerate_icons()
		H.AddSpell(new /obj/effect/proc_holder/spell/self/claws/gnoll)
		H.AddSpell(new /obj/effect/proc_holder/spell/self/howl/gnoll)
		H.AddComponent(/datum/component/gnoll_combat_tracker)

		var/obj/effect/proc_holder/spell/invoked/gnoll_sniff/F = new()
		var/obj/effect/proc_holder/spell/invoked/invisibility/gnoll/I = new()
		I.sniff_spell = F // Link them

		var/obj/effect/proc_holder/spell/invoked/abduct/S = new /obj/effect/proc_holder/spell/invoked/abduct()
		S.destination_turf = get_turf(H) // Set the anchor to where they spawn/don the outfit
		H.AddSpell(S)
		H.AddSpell(F)
		H.AddSpell(I)

		var/mode = SSgnoll_scaling.get_gnoll_scaling()
		if(mode == GNOLL_SCALING_DYNAMIC)
			to_chat(H, span_bignotice("I can expect to be joined by my pack this week. I should wait for them and group up."))
		else
			to_chat(H, span_bignotice("Isolated from my pack, I am likely a lone soul this week. I should especially avoid getting killed, and look for my pack next week."))
		to_chat(H, span_bignotice("Graggar is patient, and values good strategy. I mustn't be hasty, especially if my marks prove difficult to isolate.\n Perhaps there is merit in forging alliances, or setting up camp."))
		spawn(50)
			var/name_choice = alert(H, "What name do you want?", "MY NAME IS [H.real_name]", "Pick New Name", "Random Gnoll Name", "Keep Current Name")
			switch(name_choice)
				if("Pick New Name")
					H.choose_name_popup("GNOLL")
					to_chat(H, span_notice("Your name is now [H.real_name]."))
				if("Random Gnoll Name")
					H.real_name = "[pick(GLOB.wolf_prefixes)] [pick(GLOB.wolf_suffixes)]"
					to_chat(H, span_notice("Your name is now [H.real_name]."))
				if("Keep Current Name")
					to_chat(H, span_notice("You keep your name as [H.real_name]."))

/// Population-scaled gnoll count for a scaling mode, capped at the mode's maximum (DYNAMIC 3, FLAT 2, SINGLE 1,
/// NONE 0). Scales with population like wretch slots (+1 per 10 players above 40), just clamped lower.
/proc/gnoll_scaled_slots(mode)
	var/max_slots = 0
	switch(mode)
		if(GNOLL_SCALING_DYNAMIC)
			max_slots = 3
		if(GNOLL_SCALING_FLAT)
			max_slots = 2
		if(GNOLL_SCALING_SINGLE)
			max_slots = 1
	if(max_slots <= 0)
		return 0
	return SSgamemode.storyteller_scale_slots(max_slots)

/proc/gnollslot_calc()
	var/list/result = list()
	if(is_storyteller_soft_antag_blocked())
		result["final_slots"] = 0
		return result
	if(SSgamemode.current_storyteller?.preferred_gnoll_mode == GNOLL_SCALING_NONE)
		result["final_slots"] = 0
		return result
	var/mode = SSgnoll_scaling ? SSgnoll_scaling.get_gnoll_scaling() : GNOLL_SCALING_SINGLE
	result["final_slots"] = gnoll_scaled_slots(mode)
	return result

/proc/gnollslot_update()
	var/datum/job/gnoll_job = SSjob.GetJob("Gnoll")
	if(!gnoll_job)
		return
	if(gnoll_job.admin_slot_override)
		return
	var/admin_slot = !SSgamemode.allow_vote ? SSgamemode.admin_slots["Gnoll"] : null
	if(!isnull(admin_slot))
		gnoll_job.total_positions = max(gnoll_job.current_positions, max(0, admin_slot))
		gnoll_job.spawn_positions = max(gnoll_job.current_positions, max(0, admin_slot))
		return
	var/list/scaling = gnollslot_calc()
	var/slots = max(0, scaling["final_slots"])
	gnoll_job.total_positions = max(gnoll_job.current_positions, slots)
	gnoll_job.spawn_positions = max(gnoll_job.current_positions, slots)

/mob/living/carbon/human/proc/gnoll_inspect_skin()
	set name = "Inspect Pelt"
	set category = "RoleUnique.Gnoll"
	set desc = "Examine your gnoll skin armor"
	if(!istype(skin_armor, /obj/item/clothing/suit/roguetown/armor/regenerating/skin/gnoll_armor))
		to_chat(src, span_warning("You don't have any gnoll skin armor to inspect!"))
		return
	var/obj/item/clothing/suit/roguetown/armor/regenerating/skin/gnoll_armor/GA = skin_armor
	GA.Topic(null, list("inspect" = "1"), src)
